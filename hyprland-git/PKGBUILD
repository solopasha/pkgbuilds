pkgbase=hyprland-git
pkgname=(hyprland-git hyprland-git-devel)
pkgver=0.26.0+r27+g4afeedbd
pkgrel=1
pkgdesc="Dynamic tiling Wayland compositor that doesn't sacrifice on its looks"
arch=(x86_64)
url="https://github.com/hyprwm/Hyprland"
license=(BSD)
depends=(libdisplay-info libinput libliftoff libxcomposite libxkbcommon mesa
		pango seatd systemd xcb-util xcb-util-errors xcb-util-keysyms xcb-util-renderutil 
		xcb-util-wm xorg-xinput xorg-xwayland)
makedepends=(git cmake meson vulkan-headers glslang wayland-protocols jq)
source=("${pkgname%-git}::git+https://github.com/hyprwm/Hyprland.git"
		"git+https://github.com/hyprwm/hyprland-protocols.git"
		"git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
		"git+https://github.com/canihavesomecoffee/udis86.git")
conflicts=("${pkgname%-git}")
provides=(hyprland)
sha256sums=('SKIP'
			'SKIP'
			'SKIP'
			'SKIP')

pkgver() {
	cd "${pkgname%-git}"
  	git describe --tags --long | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

_pick() {
  local p="$1" f d; shift
  for f; do
	d="$srcdir/$p/${f#$pkgdir/}"
	mkdir -p "$(dirname "$d")"
	mv "$f" "$d"
	rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
	cd "${pkgname%-git}"
	git submodule init
	git config submodule.subprojects/wlroots.url "$srcdir/wlroots"
	git config submodule.subprojects/hyprland-protocols.url "$srcdir/hyprland-protocols"
	git config submodule.subprojects/udis86.url "$srcdir/udis86"
	git -c protocol.file.allow=always submodule update
}

build() {
	cd "${pkgname%-git}"
	arch-meson build
	meson compile -C build
}

package_hyprland-git() {
	export CFLAGS+=' -ffat-lto-objects'
	export CXXFLAGS+=' -ffat-lto-objects'
	cd "${pkgname%-git}"
	meson install -C build --destdir "$pkgdir"
	(
		cd "$pkgdir"
		_pick devel usr/include
		_pick devel usr/lib
		_pick devel usr/share/{pkgconfig,hyprland-protocols}
	)
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/hyprland-git"

}

package_hyprland-git-devel() {
	pkgdesc+=" - development files (for plugins)"

	mv devel/* "$pkgdir"
	mv "$pkgdir/usr/include/wlr" "$pkgdir/usr/include/hyprland/wlroots"
	rm -rf "$pkgdir/usr/lib"
}
