pkgbase=hyprland-git
pkgname=(hyprland-git hyprland-git-devel)
pkgver=v0.24.0+r2+g86852cdc
pkgrel=1
pkgdesc="Dynamic tiling Wayland compositor that doesn't sacrifice on its looks"
arch=("x86_64")
url="https://github.com/hyprwm/Hyprland"
license=('BSD')
depends=(mesa xcb-util xcb-util-keysyms libinput libxcomposite xorg-xinput
		libxkbcommon pango xcb-util-renderutil xcb-util-wm seatd xcb-util-errors
		libdisplay-info libliftoff)
makedepends=(git cmake meson vulkan-headers glslang systemd wayland-protocols
			xorg-xwayland jq)
source=("${pkgname%-git}::git+https://github.com/hyprwm/Hyprland.git"
		"git+https://github.com/hyprwm/hyprland-protocols.git"
		"git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
		"git+https://github.com/canihavesomecoffee/udis86.git")
sha256sums=('SKIP'
			'SKIP'
			'SKIP'
			'SKIP')

pkgver() {
	cd "${pkgname%-git}"
  	git describe --tags --long | sed 's/[^-]*-g/r&/;s/-/+/g'
}

_pick() {
  local p="$1" f d; shift
  for f; do
	d="$srcdir/$p/${f#$pkgdir/}"
	mkdir -p "$(dirname "$d")"
	mv "$f" "$d"
	rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
	cd "${pkgname%-git}"
	git submodule init
	git config submodule.subprojects/wlroots.url "$srcdir/wlroots"
	git config submodule.subprojects/hyprland-protocols.url "$srcdir/hyprland-protocols"
	git config submodule.subprojects/udis86.url "$srcdir/udis86"
	git -c protocol.file.allow=always submodule update
}

build() {
	cd "${pkgname%-git}"
	arch-meson build
	meson compile -C build
}

package_hyprland-git() {
	export CFLAGS+=' -ffat-lto-objects'
	export CXXFLAGS+=' -ffat-lto-objects'
	cd "${pkgname%-git}"
	meson install -C build --destdir "$pkgdir"
	(
		cd "$pkgdir"
		_pick devel usr/include
		_pick devel usr/lib
		_pick devel usr/share/{pkgconfig,protocols}
	)
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/hyprland-git"

}

package_hyprland-git-devel() {
	pkgdesc+=" - development files"
	depends=(hyprland)
	conflicts=(wlroots)

	mv devel/* "$pkgdir"
}
