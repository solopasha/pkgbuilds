# Maintainer: solopasha <daron439 at gmail dot com>

# broken for now :(

pkgname=remnote
pkgver=1.9.31
pkgrel=1
pkgdesc="All-in-one workspace for note-taking, learning, organizing thoughts and growing knowledge"
arch=('x86_64')
url="https://www.remnote.com"
license=('custom:Commercial')
options=(!strip)
_appimage="RemNote-${pkgver}.AppImage"
source=("https://download.remnote.io/remnote-desktop/${_appimage}"
	"${pkgname}.sh")
noextract=("${_appimage}")
sha256sums=('300c4f7426bde70886e59967a5793612876b66e0f992ee6888ce1323edeb3b2e'
            '39f1ec6a11921ee21607fb6a577871351c1875cb0966f2f98712335f0343120a')
prepare() {
	chmod a+x $_appimage
	./$_appimage --appimage-extract
	sed -i -e "s|^Exec=.*|Exec=/usr/bin/$pkgname %U|" \
		-e '/^X-AppImage-Version=.*/d' \
		-e '/Categories=/s/=/&Office;/' squashfs-root/${pkgname}.desktop
}

package() {
	depends=('electron')
	install -Dm755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 "squashfs-root/resources/app.asar" -t "${pkgdir}/usr/lib/${pkgname}"
	install -Dm644 "squashfs-root/remnote.png" -t "${pkgdir}/usr/share/pixmaps"
	install -Dm644 "squashfs-root/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
	cd squashfs-root/resources
    find . -type d -exec install -d {,"$pkgdir"/usr/lib/${pkgname}/}{} \;
    find . -type f -exec install -D {,"$pkgdir"/usr/lib/${pkgname}/}{} \;
}
